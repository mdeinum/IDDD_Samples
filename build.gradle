allprojects {
    // Define repositorie to get dependencies
    repositories {
        mavenLocal()
        mavenCentral()
    }
    configurations {

        all.collect { configuration ->
            configuration.exclude group: 'commons-logging'
        }

        dependencyManagement {
            canBeConsumed = false
            canBeResolved = false
            visible = false
        }

        matching {
            it.name.endsWith('Classpath') || it.name.contains('providedCompile')
        }.all { it.extendsFrom(dependencyManagement) }
    }

}

subprojects { subproject ->

    if (subproject.name != 'iddd_platform') {

        apply plugin: 'java'

        defaultTasks 'build'

        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(23)
            }
        }

        tasks {
            withType(JavaCompile).configureEach {
                options.compilerArgs.add('-Xlint:all')
                options.compilerArgs.add('-Xlint:-processing')
                options.compilerArgs.add('--enable-preview')
                options.compilerArgs.add('-parameters')
            }

            withType(Test).configureEach {
                useJUnitPlatform()
                jvmArgs += '--enable-preview'
                systemProperty("java.awt.headless", "true")
            }

            withType(Jar).configureEach {
                duplicatesStrategy = DuplicatesStrategy.WARN
            }
        }

        dependencies {

            dependencyManagement(enforcedPlatform(dependencies.project(path: ':iddd_platform')))

            // Logging, route everything through SLF4J
            implementation group: 'org.slf4j', name: 'slf4j-api'
            runtimeOnly group: 'org.slf4j', name: 'slf4j-simple'


            // Add dependencies for testing (these will only be used for the testing phase)
            testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter'

            testImplementation group: 'org.mockito', name: 'mockito-core'
            testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter'
            testImplementation group: 'org.springframework', name: 'spring-test'

            if (subproject.name != 'iddd_common') {
                implementation project(':iddd_common')
                testImplementation(testFixtures(project(":iddd_common")))
            }

        }
    }
}
